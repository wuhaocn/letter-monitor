buildscript {
    project.ext {

        uploadRepo = { project ->
            nexusRoot = 'https://s01.oss.sonatype.org/content/repositories'
            return {
                url "$nexusRoot/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
                credentials { username ossrhUsername; password ossrhPassword }
            }
        }
        repoClosure = {
            mavenCentral()
            maven { url "https://repo.spring.io/snapshot/" }
            maven { url "https://repo1.maven.org/maven2/" }

            mavenLocal()

        }
        versionTail = new Date().format('yyMMddHHmm')
    }
    repositories repoClosure
}


allprojects { project ->

    project.repositories rootProject.repoClosure

    group = 'io.github.helium-cloud'
    version = "1.0.0-SNAPSHOT"
}

def mavenPublishIgnoreList = ['letter-sample']

subprojects { project ->

    apply plugin: 'java'

    apply plugin: 'maven-publish'

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    tasks.withType(Test) {
        defaultCharacterEncoding = "UTF-8"
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.20"
        annotationProcessor "org.projectlombok:lombok:1.18.20"

        testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${junit_version}"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
        testImplementation "org.mockito:mockito-core:${mockito_version}"
        testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
        testImplementation "org.powermock:powermock-reflect:2.0.7"
    }
    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    if (!(project.name in mavenPublishIgnoreList)) {
        publishing {
            repositories {
                maven uploadRepo(project)
            }
            publications {
                maven(MavenPublication) {
                    from components.java
                    artifact sourceJar
                }
            }
        }
    }
}
